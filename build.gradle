/*
 *    Copyright 2020  Wu Weijie
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 *
 */

plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'signing'
}

group 'vip.wuweijie.camel'
version '3.2.0-0.0.2'

sourceCompatibility = 1.8

ext {
    camelCoreVersion = '3.2.0'
}

repositories {
    mavenLocal()
    maven { url 'https://maven.aliyun.com/repository/central' }
    mavenCentral()
}

dependencies {
    implementation "org.apache.camel:camel-core:${camelCoreVersion}"
    implementation "org.apache.rocketmq:rocketmq-client:4.7.0"
    testImplementation 'junit:junit:4.13'
    testImplementation "org.apache.camel.springboot:camel-spring-boot-starter:${camelCoreVersion}"
    testImplementation "io.hawt:hawtio-springboot:2.9.1"
    testImplementation "org.apache.camel:camel-test-spring:${camelCoreVersion}"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'camel-rocketmq'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'camel-rocketmq'
                description = 'Camel RocketMQ Component'
                url = 'https://github.com/TeslaCN/camel-rocketmq'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'TeslaCN'
                        name = 'wuweijie'
                        email = 'wuweijie.io@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/TeslaCN/camel-rocketmq.git'
                    developerConnection = 'scm:git:ssh://github.com/TeslaCN/camel-rocketmq.git'
                    url = 'https://github.com/TeslaCN/camel-rocketmq'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username System.getenv("NEXUS_USERNAME")
                password System.getenv("NEXUS_PASSWORD")
            }
        }
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Camel RocketMQ',
                'Implementation-Version': archiveVersion
    }
}
signing {
    sign publishing.publications.mavenJava
}
